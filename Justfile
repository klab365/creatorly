set quiet

CMD := if path_exists('/.dockerenv') == "false" { 'docker run --rm -v $(pwd):/workspaces/creatorly -w /workspaces/creatorly rust-toolchain cargo' } else { 'cargo' }
CMD_TTY := if path_exists('/.dockerenv') == "false" { 'docker run -it --rm -v $(pwd):/app -w /app rust-toolchain cargo' } else { 'cargo' }

# build the docker image for ci
build-docker id:
    echo "Building Docker image..."
    docker build -t rust-toolchain -f .devcontainer/Dockerfile --build-arg UID={{ id }} .

# build the code
build:
    echo "Building..."
    {{ CMD }} build

# build for release
release version='0.0.0' *ARGS='':
    echo "Building for release V{{ version }}"
    sed -i 's/^version = ".*"$/version = "{{ version }}"/' Cargo.toml
    {{ CMD }} build --release {{ARGS}}

check-format:
    echo "Checking formatting..."
    {{ CMD }} fmt --all -- --check

# format the code
format:
    echo "Formatting..."
    {{ CMD }} fmt --all

# lint the code
lint:
    echo "Linting..."
    {{ CMD }} clippy --all-targets --all-features -- -D warnings

# fix the code
fix:
    echo "Fixing..."
    {{ CMD }} fix

# run the tests
test:
    echo "Testing..."
    {{ CMD }} test

# generate coverage
coverage:
    echo "Generating coverage..."
    {{ CMD }} llvm-cov --lcov --output-path lcov.info

# run the example
example:
    echo "Running example..."
    rm -rf target/new_project
    {{ CMD_TTY }} run -- template generate local -t assets/example_project -d target/new_project
